#!/usr/bin/perl -W
#------------------------------------------------------------------------
# (c) erwan@mas.nom.fr 2007-2010
#
#  SyncDevice 
#  $Id: SyncDevice,v 1.15 2011/08/27 19:46:14 mas Exp $
#
#  Synchronize device(s) via network , with minimun bandwith .
#  The programm calculate checksum of piece of device , and 
#  if the checksum is the same there are no bandwith used .
#
# Copyright:
#  The program is distributed under the GNU General Public License version 2 
#
# Usage:
#  SyncDevice --listen --verbose
#  &
#  SyncDevice --server <hostname> --device <localdevice>
#
# Done:
#  add Zlib compression
#  block writer is asynchrone
#  add a option for the port 
# Todo:
#  Switch to C
#  add SHA-512 
#  stats - block transfert by network - compression factor 
#  
#------------------------------------------------------------------------
use strict ;
use Fcntl ;
use threads;
use threads::shared;
use Thread::Queue ;
use IO::Socket; 
use Digest::SHA1  qw(sha1_hex) ;
use Digest::MD5   qw(md5_hex) ;
use Time::HiRes qw(usleep) ;
use File::Basename ;
use English;
use Getopt::Long;
use Data::Dumper ;
use Compress::Zlib ;
$|=1;
#------------------------------------------------------------------------
my %debug_level_2_print=( 'debug'   => 5,
                          'notice'  => 4,
                          'info'    => 3,
                          'warning' => 2,
                          'error'   => 1,
                          'bogus'   => 0 );
my $debug=0 ;
#
my %options ;
GetOptions (\%options,"verbose:s","listen=s","device=s","destination-server=s","offset=i","blocksize=i","toleratefile","compress=s","truncate","bufferfactor=i");
#
if ( defined($options{'verbose'} ) ) {
    if ( $debug_level_2_print{$options{'verbose'}} ) {
	$debug=$debug_level_2_print{$options{'verbose'}} ;
    } else {
	$debug=4 ;
    }
}
#------------------------------------------------------------------------
if ( ! $options{'listen'} ) {
    if ( ! $options{'destination-server'} ||  ! $options{'device'} ) {
	die(" need a --device / --destination-server options\n") ;
    }
}
if ( ! $options{'bufferfactor'} ) {
    $options{'bufferfactor'}=1 ;
}
if ( ! $options{'offset'} ) {
    $options{'offset'}=0 ;
}
if ( ! $options{'blocksize'} ) {
    $options{'blocksize'}=1024*1024 ;
}
if ( ( ! $options{'compress'} ) || ( $options{'compress'} eq 'yes' ) ) {
    $options{'compress'}=1 ;
} else {
    $options{'compress'}=0 ;
}
#------------------------------------------------------------------------
my %block_checksum    : shared ;
my $sync_pos          : shared ;
my $sync_length       : shared ;
my $sync_length_block : shared ;
my $cksum_pos         : shared ;
my $count_ok ;
my $count_ko ;
my $sync_starttime ;
my $sync_curtime ;
my $sync_endtime ;
#
my $queue_4_writing_block=Thread::Queue->new;
#
my $thr_cksum ;
my $thr_write ;
#
if ( ! $options{'listen'} ) {
    if ( ! -b $options{'device'} && ! $options{'toleratefile'} ) {
	die(sprintf("device %s is not a device \n",$options{'device'})) ;
    } else {
	($sync_length,$sync_length_block)=DeviceSize($options{'device'},1) ;
    }
    if ( ( $sync_length % $options{'blocksize'} ) ne 0 ) {
	if ( $options{"truncate"} ) {
	    $sync_length=$sync_length-( $sync_length % $options{'blocksize'} ) ;
	    $sync_length_block=$sync_length/512 ;
	} else {
	    die(sprintf("device %s has a length that is not modulus of %d \n",$options{'device'},$options{'blocksize'})) ;	
	}
    }
    if ( $options{'destination-server'} =~ /:/ ) {
	$options{'destination-server'}=$PREMATCH ;
	$options{'destination-port'}=$POSTMATCH ;
    } else {
	$options{'destination-port'}=7070 ;
    }
} else {
    if ( $options{'listen'} =~ /:/ ) {
	$options{'listen'}=$PREMATCH ;
	$options{'port'}=$POSTMATCH ;
    } else {
	$options{'port'}=7070 ;
    }
}
#------------------------------------------------------------------------
if ( $options{'listen'} ) {
    my $sock = new IO::Socket::INET ( LocalHost => $options{'listen'} , 
				      LocalPort => $options{'port'} , ,
				      Proto => 'tcp',
				      Listen => 1, 
				      Reuse => 1 );
    #--------------------------------------
    if ( ! $sock ) {
	die "Could not create socket: $!\n" ;
    }
    &Debug("Listen on %s:%s",$options{'listen'},$options{'port'}) ;
    #--------------------------------------------------------------------
    my $state=0 ;
    my $device_local="" ;
    my $device_local_offset=0 ;
    my $device_distant="" ;
    #
    while ( $state == 0 ) {
	my $new_sock = $sock->accept(); 
	binmode $new_sock ;
	select($new_sock) ; $|=1 ; select(STDOUT) ;
	while(<$new_sock>) {
	    my $l=$_ ;
	    chomp($l) ;
	    if ( $l =~ /^DEVICE ([^\s]+)\s+(\d+)\s+(\d+)\s*$/ ) {
		$device_distant=$1 ;
		$sync_length=$2 ;
		$options{'blocksize'}=$3 ;
		#
		printf "request for sync device %s\n",$device_distant ;
		$device_local=&ReadDevice("please enter your local device:\n") ;
		$device_local_offset=&ReadOffset("please enter the current offset (0,32256,...) for synchronize:\n") ;
		if ( $device_local && defined($device_local_offset) ) {
		    my ($sd,$sdb)=&DeviceSize($device_local) ;
		    if ( ( $sync_length + $device_local_offset ) < $sd ) {
			#-------------------------------------------------
			SendCmd($new_sock,'OK DEVICE') ;
			$sync_pos=0 ;
			$count_ok=0;
			$count_ko=0;
			$sync_starttime=time();
			$sync_curtime=time()+90 ;
			#-------------------------------------------------
			$thr_cksum=threads->new(\&ComputeChecksum,$device_local,$device_local_offset,$sync_length) ;
			$thr_write=threads->new(\&BlockWriter,$device_local,$device_local_offset,$sync_length) ;
			#-------------------------------------------------
		    } else {
			&Debug("[debug] pb with request ( %d + %d >= %d )\n", $sync_length , $device_local_offset , $sd ) ;
			SendCmd($new_sock,'KO DEVICE') ;
		    }
		} else {
		    &Debug("[debug] pb with '%s' & '%s' \n",$device_local,$device_local_offset) ;
		    SendCmd($new_sock,'KO DEVICE') ;
		}
		#-------------------------------------------------------
	    } elsif ($l=~ /^\s*CKSUM\s+(\d+)\s+:([^:]+):([^:+]+)\s*/ ) {
		#---------------------------
		my $cur_pos=$1 ;
		my $cur_md5=$2 ;
		my $cur_sha1=$3 ;
		#---------------------------
		Debug("[debug] reception of CKSUM %s %s:%s (currently cksumpos %s) \n",$cur_pos,$cur_md5,$cur_sha1,$cksum_pos) ;
		if ( ! $block_checksum{$cur_pos} ) {
		    sleep 1 ;
		}
		while ( ! $block_checksum{$cur_pos} ) {
		    Debug('[notice] ComputeChecksum too slow / Waiting %s vs %s (%s) ',$cur_pos,$cksum_pos,scalar(keys %block_checksum)) ;
		    sleep 1 ;
		}
		Debug("[debug] ok Got IT ! cksumpos %s\n",$cksum_pos) ;
		#---------------------------
		if ( ( $block_checksum{$cur_pos}->{'md5'} eq $cur_md5 ) &&
		     ( $block_checksum{$cur_pos}->{'sha1'} eq $cur_sha1 ) ) {
		    $count_ok++ ;
		    SendCmd($new_sock,"BLOCK $cur_pos OK") ;
		    lock($sync_pos) ;
		    $sync_pos+=$options{'blocksize'} ;
		    PurgeBlockCheckSum($sync_pos);
		} else {
		    SendCmd($new_sock,"NEED DATA $cur_pos") ;		    
		    $count_ko++ ;
		}
		#-------------------------------------------------------
	    } elsif ($l=~ /^\s*BLOCK DATA\s+(\d+)\s*/ ) {
		#---------------------------
		Debug("[debug] %s .... currently receive ....",$l) ;
		my $dat_pos=$1 ;
		my $buf ;
		my $nr=read($new_sock,$buf,$options{'blocksize'} ) ;
		if ( $nr == $options{'blocksize'} ) {
		    Debug("[debug] .... currently receive .... DONE ") ;
		    while ( $queue_4_writing_block->pending() > 60 ) {
			sleep(2) ;
			Debug('[notice] BlockWriter too slow') ;
		    }
		    $queue_4_writing_block->enqueue('WRT') ;
		    $queue_4_writing_block->enqueue($dat_pos+$device_local_offset) ;
		    $queue_4_writing_block->enqueue($buf) ;
		    lock($sync_pos) ;
		    $sync_pos=$dat_pos+$options{'blocksize'} ;
		    PurgeBlockCheckSum($sync_pos);
		    Debug("[debug] .... sync local .... DONE ") ;
		} else {
		    Debug("[error] block ${dat_pos} length read data <> blocksize") ;
		}
		$buf=undef ;
		#-------------------------------------------------------
	    } elsif ($l=~ /^\s*BLOCK DATA\s+COMPRESS\s+(\d+)\s+(\d+)\s*/ ) {
		#---------------------------
		Debug("[debug] %s .... currently receive ....",$l) ;
		my $dat_pos=$1 ;
		my $dat_siz=$2 ;
		my $buf_cmp ;
		my $nr=read($new_sock,$buf_cmp,$dat_siz) ;
		if ( $nr == $dat_siz ) {
		    Debug("[debug] .... currently receive .... DONE ") ;
		    while ( $queue_4_writing_block->pending() > 60 ) {
			sleep(2) ;
			Debug('[notice] BlockWriter too slow') ;
		    }
		    my $buf=uncompress($buf_cmp) ;
		    if ( length($buf) == $options{'blocksize'} ) {
			$queue_4_writing_block->enqueue('WRT') ;
			$queue_4_writing_block->enqueue($dat_pos+$device_local_offset) ;
			$queue_4_writing_block->enqueue($buf) ;
			lock($sync_pos) ;
			$sync_pos=$dat_pos+$options{'blocksize'} ;
			PurgeBlockCheckSum($sync_pos);
			Debug("[debug] .... sync local .... DONE ") ;
		    } else {
			Debug("[error] block ${dat_pos} length uncompress block <> blocksize") ;
		    }
		    $buf=undef ;
		}
		$buf_cmp=undef ;
		#-------------------------------------------------------
	    } else {
		#-------------------------------------------------------
		print unpack( 'H*', $l),"\n" ; 
		print join(' ',split(//,$l)),"\n" ; 
		#-------------------------------------------------------
	    }
	    #------------------------------------------------------------
	    if ( $sync_pos >= $sync_length ) {
		$sync_endtime=time() ;
		Debug("OK sync done : sync_pos %s sync_length %s block_ok %s block_ko %s timing %s \n",
		      $sync_pos,$sync_length,$count_ok,$count_ko,($sync_endtime-$sync_starttime) ) ;
	    }
	    #------------------------------------------------------------
	    if ( $sync_curtime < time()  ) {
		$sync_curtime=time()+90 ;
		Debug("Stats Sync : sync_pos %s sync_length %s block_ok %s block_ko %s timing %s remaining time %s\n",
		      $sync_pos,$sync_length,$count_ok,$count_ko,(time()-$sync_starttime),
		      int(((time()-$sync_starttime)/ ($sync_pos+1))*$sync_length)-(time()-$sync_starttime) ) ;
	    }
	    #------------------------------------------------------------
	    $l=undef ;
	    #------------------------------------------------------------
	}
	#----------------------------------------------------------------
	if ( ! $sync_endtime ) {
	    $sync_endtime=time() ;
	}
	Debug("OK connection closed \n") ;
	Debug("OK sync abort : sync_pos %s sync_length %s block_ok %s block_ko %s timing %s\n",
	             $sync_pos,$sync_length,$count_ok,$count_ko,($sync_endtime-$sync_starttime)) ;
	#----------------------------------------------------------------
    }
    close($sock);
    #--------------------------------------------------------------------
} else {
    #--------------------------------------------------------------------
    use IO::Socket;
    my $sock = new IO::Socket::INET ( PeerAddr => $options{'destination-server'} , 
				      PeerPort => $options{'destination-port'} , , 
				      Proto => 'tcp' ); 
    if ( ! $sock ) {
	die "Could not connect to ".$options{'destination-server'}.":".$options{'destination-port'}." ( create socket: $! ) \n" ;
    }
    binmode $sock ;
    select($sock) ; $|=1 ; select(STDOUT) ;
    #--------------------------------------------------------------------
    if ( SendCmdAndWait($sock,sprintf("DEVICE %s %s %s",$options{'device'},$sync_length,$options{'blocksize'})) =~/^\s*OK DEVICE\s*$/ ) {
	Debug("OK SendCmdAndWait DEVICE\n") ;
	#
	$sync_pos=0 ;
	$thr_cksum=threads->new(\&ComputeChecksum,$options{'device'},$options{'offset'},$sync_length) ;
	#
	while ( $sync_pos < $sync_length ) {
	    if ( $block_checksum{$sync_pos} ) {
		my $r=SendCmdAndWait($sock,sprintf("CKSUM %s :%s:%s:",$sync_pos,
						   $block_checksum{$sync_pos}->{'md5'},$block_checksum{$sync_pos}->{'sha1'})) ;
		if ( $r =~ /^\s*BLOCK $sync_pos OK\s*$/ ) {
		    lock($sync_pos) ;
		    $sync_pos+=$options{'blocksize'} ;
		} elsif ( $r =~ /^\s*NEED DATA $sync_pos\s*$/ ) {
		    &SendBlockData($sock,$options{'device'},$sync_pos) ;
		    lock($sync_pos) ;
		    $sync_pos+=$options{'blocksize'} ;
		}
		PurgeBlockCheckSum($sync_pos);
	    } else {
		if ( defined ( $cksum_pos ) ) {
		    Debug('[notice] ComputeChecksum too slow / Waiting %s vs %s (%s) ',$cksum_pos,$sync_pos,scalar(keys %block_checksum)) ;
		}
		sleep(1) ;
	    }
	}
	#
    } else {
	Debug("PB sending DEVICE\n") ;
    }
    #
    sleep(3600*10) ;
    #--------------------------------------------------------------------
    close($sock);
    #--------------------------------------------------------------------
}
#----------------------------------------------------------------------------------------------
sub BlockWriter {
    my ($dev,$dev_off,$dat_len)=@_ ;
    my $last_write=0 ;
    while ( $last_write < $dev_off+$dat_len-$options{'blocksize'} ) {
	my $what=$queue_4_writing_block->dequeue() ;
	if ( $what eq 'WRT' ) {
	    my $pos=$queue_4_writing_block->dequeue() ;
	    my $dat=$queue_4_writing_block->dequeue() ;
	    &WriteBlockData($dev,$pos,$dat) ;
	    $last_write=$pos ;
	    $pos=undef ;
	    $dat=undef ;
	} else {
	    Debug("[error] BlockWriter bad args '%s'",$what) ;
	}
	$what=undef ;
    }
}
#----------------------------------------------------------------------------------------------
sub WriteBlockData {
    my $d=$_[0] ;
    my $p=$_[1] ;
    my $b=$_[2] ;
    #----------------------
    if ( ! sysopen(DEVTOWRITE,$d, O_LARGEFILE | O_RDWR) ) {
        Debug("PB open device '%s'\n",$d) ;
        exit 1 ;
    }
    binmode DEVTOWRITE ;
    #----------------------
    sysseek(DEVTOWRITE,$p,0) ;
    my $nwrite=syswrite(DEVTOWRITE,$b,$options{'blocksize'}) ;
    close(DEVTOWRITE) ;
    #----------------------
    if ( $nwrite != $options{'blocksize'} ) {
	use bytes ;
	Debug("PB write device '%s' at '%s' length b %s / nwrite %s \n",$d,$p,length($b),$nwrite) ;
	exit 1;
    }
    #----------------------
}
#----------------------------------------------------------------------------------------------
sub SendBlockData {
    my $s=$_[0] ;
    my $d=$_[1] ;
    my $p=$_[2] ;
    #----------------------
    if ( ! open(DEVTOREAD,"<$d") ) {
        Debug("PB open device '%s'\n",$d) ;
        exit 1 ;
    }
    binmode DEVTOREAD ;
    #----------------------
    my $buf ;
    sysseek(DEVTOREAD,$p,0) ;
    my $nread=sysread(DEVTOREAD,$buf,$options{'blocksize'}) ;
    if ( $nread == $options{'blocksize'} ) {
	if ( ! $options{'compress'} ) {
	    print $s "BLOCK DATA $p\n" ;
	    Debug("[debug] .... starting transmiting block $p  ") ;
	    print $s $buf ;
	} else {
	    use bytes ;
	    my $buf_cmp=compress($buf) ;
	    printf $s "BLOCK DATA COMPRESS ".$p." %d \n",length($buf_cmp) ;
	    Debug(sprintf("[debug] .... starting transmiting block ".$p." compress %d",length($buf_cmp))) ;
	    print  $s $buf_cmp ;
	}
    }
    $buf=undef ;
    #----------------------
    close(DEVTOREAD) ;
    #----------------------
    Debug("[debug] .... transmiting .... DONE ") ;
}
#----------------------------------------------------------------------------------------------
sub DisplayProgress {
    #--------------------------------------------------------------------
    #--------------------------------------------------------------------    
}
#----------------------------------------------------------------------------------------------
sub PurgeBlockCheckSum {
    my $p=$_[0] ;
    #
    $p-=$options{'blocksize'} ;
    #
    while ( defined($block_checksum{$p}) ) {
	lock(%block_checksum) ;
	delete $block_checksum{$p}->{'md5'} ;
	delete $block_checksum{$p}->{'sha1'} ;
	delete $block_checksum{$p}->{'pos'} ;
	delete $block_checksum{$p} ;
    }
    #
}
#----------------------------------------------------------------------------------------------
sub ComputeChecksum {
    #--------------------------------------------------------------------
    my ($device_to_read,$device_offset,$sync_size)=($_[0],$_[1],$_[2]) ;
    Debug('starting cksum thread on %s starting at %s during %s bytes',$device_to_read,$device_offset,$sync_size) ;
    #--------------------------------------------------------------------
    if ( ! sysopen(DEVCKSUM,$device_to_read, O_LARGEFILE | O_RDONLY )  ) {
	Debug("PB open device '%s'\n",$device_to_read) ;
	exit 1 ;
    }
    $cksum_pos=0 ;
    binmode DEVCKSUM ;
    #--------------------------------------------------------------------
    my $factor=(268435456/$options{'blocksize'})*$options{'bufferfactor'} ;
    #--------------------------------------------------------------------
    while ( $cksum_pos  < $sync_size ) {
	if ( $cksum_pos > $sync_pos + $factor*$options{'blocksize'} ) {
	    Debug('[notice] ComputeChecksum too fast / Waiting %s vs %s (%s) ',$cksum_pos,$sync_pos,scalar(keys %block_checksum)) ;
	    sleep(5) ;
	} else {
	    my $buf ;
	    if ( ! sysseek(DEVCKSUM,$device_offset+$cksum_pos,0) ) {
		Debug("pb during seek at %s error %s\n",$device_offset+$cksum_pos,$!) ;
		exit 1 ;
	    }
	    my $nread=sysread(DEVCKSUM,$buf,$options{'blocksize'}) ;
	    if ( defined($nread) ) {
		if ( $nread == $options{'blocksize'} ) {
		    my %h : shared  ;
		    $h{'pos'}=$cksum_pos ;
		    $h{'md5'}=md5_hex($buf) ;
		    $h{'sha1'}=sha1_hex($buf) ;
		    lock(%block_checksum) ;
		    $block_checksum{$cksum_pos}=\%h ;
		    $cksum_pos=$cksum_pos+$options{'blocksize'} ;
		} else {
		    Debug("pb during read it got only %s instead %s\n",$nread,$options{'blocksize'}) ;
		    exit 1 ;
		}
	    } else {
		Debug("pb during read it %s error %s\n",$device_offset+$cksum_pos,$!) ;
		exit 1 ;
	    }
	    $buf=undef ;
	}
    }
    close(DEVCKSUM) ;
    #--------------------------------------------------------------------
}
#----------------------------------------------------------------------------------------------
sub DeviceSize {
    if ( -b $_[0] ) {
	my $sb =`blockdev  --getsize64 $_[0] 2>&1 ` ;
	my $sbk ;
        if ( $sb !~ /^\d+$/ ) {
	    $sb =`blockdev  --getsize $_[0] 2>&1 ` ;
	    chomp($sb) ;
	    $sbk=$sb ;
	    $sb=$sbk*512 ;
	} else {
	    $sbk=$sb/512 ;
	}
	Debug("for device %s size is  %s bytes or %s blocks(512bytes)\n",$_[0],$sb,$sbk ) ;
	return ( $sb,$sbk) ;
    } elsif ( -f $_[0] && $_[1]) {
	my @infof=(stat($_[0]))[7] ;
	if ( ( ( $infof[0] % 512 ) != 0 ) ) {
	    die(sprintf("for file %s , size in byte ( %d ) is a multiple of 512 \n",$_[0],$infof[0])) ;
	}
	push @infof,$infof[0]/512 ;
	Debug("for file %s size is  %s bytes or %s blocks(512bytes)\n",$_[0],$infof[0],$infof[1]) ;
	return @infof ;
    } else {
	return (0,0) ;
    }
}
#----------------------------------------------------------------------------------------------
sub SendCmd {
    my $s = $_[0] ;
    my $t = $_[1] ;
    #----------------------------------
    if ( print $s $t."\n" ) {
	Debug("[debug] OK SendCmd %s\n",$t) ;
	return 1 ;
    } else {
	Debug("[error] KO SendCmd %s\n",$t) ;
	return undef ;
    }
    #----------------------------------
}
#----------------------------------------------------------------------------------------------
sub SendCmdAndWait {
    my $s = $_[0] ;
    my $t = $_[1] ;
    #----------------------------------
    if ( SendCmd($s,$t) ) {
	my $l ;
	while ( ! $l ) {
	    $l=<$s> ;
	    if ( defined $l ) {
		chomp($l) ;
		Debug("[debug] SendCmdAndWait receive '%s'\n",$l) ;
		return $l ;
	    } else {
		return undef ;
	    }
	}
	return undef ;
    } else {
	return undef ;
    }
    #----------------------------------
}
#----------------------------------------------------------------------------------------------
sub ReadOffset {
    my $o ;
    print $_[0] ;
    while ( ! $o ) {
	$o=<> ;
	chomp($o) ;
	if ( $o =~ /^[0-9]+$/ ) {
	    return $o ;
	}
	$o=undef ;
	print $_[0] ;
    }
}
#----------------------------------------------------------------------------------------------
sub ReadDevice {
    my $d ;
    print $_[0] ;
    while ( ! $d ) {
	$d=<> ;
	chomp($d) ;
	if ( -b $d ) {
	    return $d ;
	}
	$d=undef ;
	print $_[0] ;
    }
}
#----------------------------------------------------------------------------------------------
# extrait de syslog.conf
# debug, info, notice, warning , error
#
sub Debug {
    #
    if (! $debug ) {
        return undef ;
    }
    #
    my $txt_to_print  ;
    #
    if ( scalar(@_) > 1 ) {
        $txt_to_print=sprintf($_[0],@_[1..(scalar(@_)-1)]) ;
    } else {
        $txt_to_print=$_[0] ;
    }
    #
    my $level ;
    if ( $txt_to_print =~ /^\s*\[((debug)|(notice)|(info)|(warning)|(error))\]\s*([^\s].*)$/i ) {
        $txt_to_print=sprintf("[%-7s] %s",uc($1),$7) ;
        $level=lc($1) ;
    } elsif ( $txt_to_print =~ /^\s*\[(\[[^\]]*\])\]\s*([^\s].*)$/i ) {
        $txt_to_print=sprintf("[BOGUS  ] %s",$txt_to_print) ;
        $level='bogus' ;
    } else {
        $txt_to_print=sprintf("[INFO   ] %s",$txt_to_print) ;
        $level='info' ;
    }
    #
    if ( $debug_level_2_print{$level} > $debug ) {
        return undef ;
    }
    #
    my $txt_time=sprintf("[%02d:%02d:%02d] ",(localtime())[2,1,0]) ;
    #
    $txt_to_print=join("\n", map {$_=$txt_time.$_ ; }  (split(/\n/,$txt_to_print)) ) ;
    #
    chomp($txt_to_print) ;
    $txt_to_print.="\n" ;
    #
    print STDERR $txt_to_print ;
    #
}
#------------------------------------------------------------------------
1;
__END__
#------------------------------------------------------------------------
scp lotar.emotic.info:/usr/local/bin/SyncDevice .
/usr/local/bin/SyncDevice --device /dev/xen_hdd/mylar --destination-server 10.3.0.1 --verbose
/dev/drbd2
#------------------------------------------------------------------------
/dev/lotar/Grub-Etherboot-ISO
lvcreate -n TestSyncDevice --size 15M xen_hdd
lvcreate -n TestSyncDevice --size 17M lotar
dd if=/dev/random of=/dev/xen_hdd/TestSyncDevice bs=512

/usr/local/bin/SyncDevice --device /dev/xen_hdd/TestSyncDevice --destination-server 10.3.0.1 --verbose
/dev/lotar/TestSyncDevice
32256

#------------------------------------------------------------------------

pb conso memoirre avec des petit blocks
pb thread de cksum qu'il faut arreter quand le client abort
prevoir une progress bar



prevoir une purge


OK SendCmd BLOCK 14898167808 OK
#------------------------------------------------------------------------
