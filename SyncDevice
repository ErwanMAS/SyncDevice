#!/usr/bin/perl -W
#------------------------------------------------------------------------
# (c) erwan@mas.nom.fr 2007-2010
#
#  SyncDevice 
#  $Id: SyncDevice,v 1.35 2014/02/26 11:44:12 mas Exp mas $
#
#  Synchronize device(s) via network , with minimun bandwith .
#  The programm calculate checksum of piece of device , and 
#  if the checksum is the same there are no bandwith used .
#
# Copyright:
#  The program is distributed under the GNU General Public License version 2 
#
# Usage:
#  SyncDevice --listen --verbose
#  &
#  SyncDevice --server <hostname> --device <localdevice>
#
# Done:
#  add Zlib compression
#  block writer is asynchrone
#  add a option for the port 
# Todo:
#  Switch to C
#  stats - block transfert by network - compression factor 
#  care , statifier => http://reproducible.io/
#------------------------------------------------------------------------
use strict ;
use Fcntl ;
use threads;
use threads::shared;
use Thread::Queue ;
use IO::Select;
use IO::Socket; 
use MIME::Base64 qw( encode_base64 ) ;
use Digest::SHA   qw(sha1 sha512) ;
use Digest::MD5   qw(md5) ;
use Digest::Alder32 ;
# use Digest::MD4   qw(md4) ;
# use Digest::EdonR qw(edonr_512 edonr_384 ) ;
# use Digest::Skein qw(skein_1024) ;
use Time::HiRes qw(usleep) ;
use File::Basename ;
use English;
use Getopt::Long;
use Data::Dumper ;
use Compress::Zlib ;
$|=1;
#------------------------------------------------------------------------
my %debug_level_2_print=( 'debug'   => 5,
                          'notice'  => 4,
                          'info'    => 3,
                          'warning' => 2,
                          'error'   => 1,
                          'bogus'   => 0 );
my $debug=0 ;
#
my %options ;
GetOptions (\%options,"verbose:s","listen=s","device=s","destination-server=s","mode=s","auto-accept","destination-device=s","destination-offset=i","filter-remote=s",
	              "offset=i","blocksize=i","toleratefile","compress=s","truncate","bufferfactor=i","stats-interval=i","sync-window=i","display-ko");
#
if ( defined($options{'verbose'} ) ) {
    if ( $debug_level_2_print{$options{'verbose'}} ) {
	$debug=$debug_level_2_print{$options{'verbose'}} ;
    } else {
	$debug=4 ;
    }
}
#------------------------------------------------------------------------
if ( ! $options{'listen'} ) {
    if ( ! $options{'destination-server'} ||  ! $options{'device'} ) {
	die(" need a --device / --destination-server options\n") ;
    }
}
if ( ! $options{'bufferfactor'} ) {
    $options{'bufferfactor'}=1 ;
}
if ( ! $options{'offset'} ) {
    $options{'offset'}=0 ;
}
if ( ! $options{'stats-interval'} ) {
    $options{'stats-interval'}=90 ;
}
if ( ! $options{'sync-window'} ) {
    $options{'sync-window'}=8192 ;
}
if ( ! $options{'mode'} ) {
    $options{'mode'}="secure" ;
} else {
    if ( ! $options{'listen'} ) {
	if ( ! ( ( $options{'mode'} eq 'secure' ) || ( $options{'mode'} eq 'fast' ) || ( $options{'mode'} eq 'ultra' ) || ( $options{'mode'} eq 'standart' ) ) ) {
	    die(sprintf("mode is not secure and not fast and not ultra and not standart .\n",$options{'mode'})) ;
	}
    } else {
	die("option mode is not available in daemon mode.\n") ;
    }
}
if ( ! $options{'blocksize'} ) {
    $options{'blocksize'}=1024*1024 ;
}
if ( ( ! $options{'compress'} ) || ( $options{'compress'} eq 'yes' ) ) {
    $options{'compress'}=1 ;
} else {
    $options{'compress'}=0 ;
}
#------------------------------------------------------------------------
my %block_checksum    : shared ;
my $sync_pos          : shared ;
my $sync_length       : shared ;
my $sync_length_block : shared ;
my $cksum_pos         : shared ;
my $count_ok ;
my $count_ko ;
my $list_ko ;
my $network_bytes_receive ;
my $network_block_receive ;
my $sync_starttime ;
my $sync_curtime ;
my $sync_endtime ;
#
my $queue_4_writing_block=Thread::Queue->new;
my $queue_4_send_packet=Thread::Queue->new;
#
my $thr_cksum ;
my $thr_write ;
#
if ( ! $options{'listen'} ) {
    if ( ! -b $options{'device'} && ! $options{'toleratefile'} ) {
	die(sprintf("device %s is not a device \n",$options{'device'})) ;
    } else {
	($sync_length,$sync_length_block)=DeviceSize($options{'device'},1) ;
    }
    if ( ( $sync_length % $options{'blocksize'} ) ne 0 ) {
	if ( $options{"truncate"} ) {
	    $sync_length=$sync_length-( $sync_length % $options{'blocksize'} ) ;
	    $sync_length_block=$sync_length/512 ;
	} else {
	    die(sprintf("device %s has a length that is not modulus of %d \n",$options{'device'},$options{'blocksize'})) ;	
	}
    }
    if ( $options{'destination-server'} =~ /:/ ) {
	$options{'destination-server'}=$PREMATCH ;
	$options{'destination-port'}=$POSTMATCH ;
    } else {
	$options{'destination-port'}=7070 ;
    }
} else {
    if ( $options{'listen'} =~ /:/ ) {
	$options{'listen'}=$PREMATCH ;
	$options{'port'}=$POSTMATCH ;
    } else {
	$options{'port'}=7070 ;
    }
}
#------------------------------------------------------------------------
if ( $options{'listen'} ) {
    my $sock = new IO::Socket::INET ( LocalHost => $options{'listen'} , 
				      LocalPort => $options{'port'} , ,
				      Proto => 'tcp',
				      Listen => 1, 
				      Reuse => 1 );
    #--------------------------------------
    if ( ! $sock ) {
	die "Could not create socket: $!\n" ;
    }
    &Debug("Listen on %s:%s",$options{'listen'},$options{'port'}) ;
    #--------------------------------------------------------------------
    my $state=0 ;
    my $device_local=undef ;
    my $device_local_offset=undef ;
    my $device_distant="" ;
    #
    while ( $state == 0 ) {
	my ($new_sock,$remote_addr) = $sock->accept(); 
	#----------------------------------------------------------------
	my ($port, $ip) = sockaddr_in $remote_addr;
	my $ipnum       = inet_ntoa   $ip;
	if ( $options{'filter-remote'} && $options{'filter-remote'} ne $ipnum ) {
	    &Debug('REJECT connection from "%s"',$ipnum) ;
	    close($new_sock);
	    next ;
	}
	#----------------------------------------------------------------
	binmode $new_sock ;
	select($new_sock) ; $|=1 ; select(STDOUT) ;
	my $sel_sock = IO::Select->new( $new_sock );
	#
	$new_sock->sockopt(SO_RCVBUF, 32*1048*1024);
	$new_sock->sockopt(SO_SNDBUF, 32*1048*1024);
	#
	&Debug("[debug] start loop on sock") ;
	while ( 1 ) {
	    #------------------------------------------------------------
	    if ( $queue_4_send_packet->pending() > 0 )  {
		if ( $sel_sock->can_write(0) ) {
		    my $str_to_write=$queue_4_send_packet->dequeue() ;
		    SendCmd($new_sock,$str_to_write) ;
		    next  ;
		} 
	    }
	    #------------------------------------------------------------	
	    if ( defined($device_local ) && $sync_pos >= $sync_length && $queue_4_send_packet->pending() == 0 ) {
		$sync_endtime=time() ;
		$queue_4_writing_block->enqueue('EOF') ;
		Debug("[debug] enqueue(EOF) .... DONE ") ;
		$thr_cksum->join() ;
		Debug("[debug] join cksum .... DONE ") ;
		$thr_write->join() ;
		Debug("[debug] join write .... DONE ") ;
		Debug("OK sync done : sync_pos %s sync_length %s block_ok %s block_ko %s timing %s \n",
		      $sync_pos,$sync_length,$count_ok,$count_ko,($sync_endtime-$sync_starttime) ) ;
		if ( $options{'display-ko'} && scalar(@{$list_ko}) ) {
		    my $str=join(',',@{$list_ko}) ;
		    $str=~ s/(.{70,90},)/$1\n/g ;
		    Debug("Display Ko:\n".$str) ;
		}
		close($new_sock);
		last ;
	    }
	    #------------------------------------------------------------	
	    my $l=ReadCmd($new_sock) ;
	    if ( ! defined($l) ) {
		last ;
	    }
	    if ( $l =~ /^DEVICE ([^\s]+)\s+(\d+)\s+(\d+)\s+((SECURE)|(FAST)|(ULTRA))(\s*|(\s+([^\s]+)\s+(\d+)\s*))$/ ) {
		$device_distant=$1 ;
		$sync_length=$2 ;
		$options{'blocksize'}=$3 ;
		$options{'mode'}=lc($4) ;
		#
		if ( defined($10) && defined($11) ) {
		    $device_local=$10 ;
		    $device_local_offset=$11 ;
		    if ( ! -b $device_local || $device_local_offset !~  /^[0-9]+$/ ) {
			$device_local=undef ;
			$device_local_offset=undef ;
		    }
		}
		#
		if ( ! ( $device_local && defined ( $device_local_offset ) && $options{'auto-accept'} ) ) {
		    printf "request for sync device %s ( blocksize %d ) in %s mode\n",$device_distant,$options{'blocksize'},$options{'mode'} ;
		    $device_local=&ReadDevice("please enter your local device:\n") ;
		    $device_local_offset=&ReadOffset("please enter the current offset (0,32256,...) for synchronize:\n") ;
		} else {
		    &Debug("[notice] request for sync device %s ( blocksize %d ) in %s mode\n",$device_distant,$options{'blocksize'},$options{'mode'} ) ;
		    &Debug("[notice] device local is %s , start at pos %s\n",$device_local,$device_local_offset ) ;
		}
		if ( $device_local && defined($device_local_offset) ) {
		    my ($sd,$sdb)=&DeviceSize($device_local) ;
		    if ( ( $sync_length + $device_local_offset ) <= $sd ) {
			#-------------------------------------------------
			&Debug("[debug] ok with request '%s' => '%s'\@%d \n", $device_distant ,$device_local,$device_local_offset) ;
			$queue_4_send_packet->enqueue('OK DEVICE') ;
			$sync_pos=0 ;
			$count_ok=0;
			$count_ko=0;
			$list_ko=[];
			$network_bytes_receive=0 ;
			$network_block_receive=0 ;
			$sync_starttime=time();
			$sync_curtime=time()+$options{'stats-interval'} ;
			#-------------------------------------------------
			$thr_cksum=threads->new(\&ComputeChecksum,$device_local,$device_local_offset,$sync_length) ;
			$thr_write=threads->new(\&BlockWriter,$device_local,$device_local_offset,$sync_length) ;
			#-------------------------------------------------
		    } else {
			&Debug("[debug] pb with request ( %d + %d > %d )\n", $sync_length , $device_local_offset , $sd ) ;
			$queue_4_send_packet->enqueue('KO DEVICE') ;
		    }
		} else {
		    &Debug("[debug] pb with '%s' & '%s' \n",$device_local,$device_local_offset) ;
		    $queue_4_send_packet->enqueue('KO DEVICE') ;
		}
		#-------------------------------------------------------
	    } elsif ($l=~ /^\s*C(\d+):([^:]+):(.+)\s*/ ) {
		#---------------------------
		my $cur_pos=$1 ;
		my $cur_md5=$2 ;
		my $cur_sha1=$3 ;
		#---------------------------
		Debug("[debug] reception of CKSUM %s %s:%s (currently cksumpos %s) \n",$cur_pos,$cur_md5,$cur_sha1,$cksum_pos) ;
		if ( ! $block_checksum{$cur_pos} ) {
		    sleep 1 ;
		}
		my $wait_loop=0 ;
		while ( ! $block_checksum{$cur_pos} ) {
		    $wait_loop++;
		    sleep 1 ;
		}
		if ( $wait_loop > 5 ) {
		    Debug('[notice] ComputeChecksum too slow / Waiting %s vs %s (%s) ',$cur_pos,$cksum_pos,scalar(keys %block_checksum)) ;
		}
		Debug("[debug] ok Got IT ! cksumpos %s\n",$cksum_pos) ;
		#---------------------------
		if ( ( $block_checksum{$cur_pos}->{'md5'} eq $cur_md5 ) &&
		     ( $block_checksum{$cur_pos}->{'sha1'} eq $cur_sha1 ) ) {
		    $count_ok++ ;
		    $queue_4_send_packet->enqueue("B$cur_pos") ;
		    lock($sync_pos) ;
		    $sync_pos+=$options{'blocksize'} ;
		    PurgeBlockCheckSum($sync_pos);
		} else {
		    $queue_4_send_packet->enqueue("NEED DATA $cur_pos") ;		    
		    $count_ko++ ;
		    push @{$list_ko} , $cur_pos ;
		}
		#-------------------------------------------------------
	    } elsif ($l=~ /^\s*BLOCK DATA\s+(\d+)\s*/ ) {
		#---------------------------
		Debug("[debug] %s .... currently receive ....",$l) ;
		my $dat_pos=$1 ;
		my $buf ;
		my $s2r=$options{'blocksize'} ;
		my $inr=sysread($new_sock,$buf,$s2r) ;
		my $nr=0 ;
		while ( $inr ) {
		    $nr+=$inr ;
		    $s2r-=$inr ;
		    if ( $s2r ) {
			$inr=sysread($new_sock,$buf,$s2r,$nr) ;
		    } else {
			$inr=0 ;
		    }
		}
		if ( $nr == $options{'blocksize'} ) {
		    $network_bytes_receive+=$options{'blocksize'} ;
		    $network_block_receive++ ;
		    Debug("[debug] .... currently receive .... DONE ") ;
		    while ( $queue_4_writing_block->pending() > 120 ) {
			sleep(2) ;
			Debug('[notice] BlockWriter too slow') ;
		    }
		    $queue_4_writing_block->enqueue('WRT') ;
		    $queue_4_writing_block->enqueue($dat_pos+$device_local_offset) ;
		    $queue_4_writing_block->enqueue($buf) ;
		    lock($sync_pos) ;
		    $sync_pos+=$options{'blocksize'} ;
		    PurgeBlockCheckSum($sync_pos);
		    Debug("[debug] .... sync local .... DONE ") ;
		} else {
		    Debug("[error] block ${dat_pos} length read data <> blocksize") ;
		}
		$buf=undef ;
		#-------------------------------------------------------
	    } elsif ($l=~ /^\s*BLOCK DATA\s+COMPRESS\s+(\d+)\s+(\d+)\s*/ ) {
		#---------------------------
		Debug("[debug] %s .... currently receive ....",$l) ;
		my $dat_pos=$1 ;
		my $dat_siz=$2 ;
		my $buf_cmp ;
		my $s2r=$dat_siz;
		my $inr=sysread($new_sock,$buf_cmp,$s2r) ;
		my $nr=0 ;
		while ( $inr ) {
		    $nr+=$inr ;
		    $s2r-=$inr ;
		    if ( $s2r ) {
			$inr=sysread($new_sock,$buf_cmp,$s2r,$nr) ;
		    } else {
			$inr=0 ;
		    }
		}
		if ( $nr == $dat_siz ) {
		    $network_bytes_receive+=$dat_siz ;
		    $network_block_receive++ ;
		    Debug("[debug] .... currently receive .... DONE ") ;
		    while ( $queue_4_writing_block->pending() > 120 ) {
			sleep(2) ;
			Debug('[notice] BlockWriter too slow') ;
		    }
		    my $buf=uncompress($buf_cmp) ;
		    if ( length($buf) == $options{'blocksize'} ) {
			$queue_4_writing_block->enqueue('WRT') ;
			$queue_4_writing_block->enqueue($dat_pos+$device_local_offset) ;
			$queue_4_writing_block->enqueue($buf) ;
			lock($sync_pos) ;
			$sync_pos+=$options{'blocksize'} ;
			PurgeBlockCheckSum($sync_pos);
			Debug("[debug] .... sync local .... DONE ") ;
		    } else {
			Debug("[error] block ${dat_pos} length uncompress block <> blocksize") ;
		    }
		    $buf=undef ;
		} else {
		    Debug("[error] block %s read %s instead %s ",$dat_pos,$nr,$dat_siz) ;
		}
		$buf_cmp=undef ;
		#-------------------------------------------------------
	    } else {
		#-------------------------------------------------------
		Debug("[error] BOGUS") ;
		print unpack( 'H*', $l),"\n" ; 
		print join(' ',split(//,$l)),"\n" ; 
		#-------------------------------------------------------
	    }
	    #------------------------------------------------------------
	    if ( $sync_curtime < time()  ) {
		$sync_curtime=time()+$options{'stats-interval'} ;
		Debug("Stats Sync : s_pos %s s_length %s b_ok %s b_ko %s b_wrt %s ack %s | time %s remaining %s | net %d bytes for %d bytes\n",
		      $sync_pos,$sync_length,$count_ok,$count_ko,$network_block_receive,$queue_4_send_packet->pending(),
		      (time()-$sync_starttime),int(((time()-$sync_starttime)/ ($sync_pos+1))*$sync_length)-(time()-$sync_starttime) ,
		      $network_bytes_receive , $count_ko*$options{'blocksize'} ) ;
	    }
	    #------------------------------------------------------------
	    $l=undef ;
	    #------------------------------------------------------------
	}
	#----------------------------------------------------------------
	if ( ! $sync_endtime ) {
	    $sync_endtime=time() ;
	}
	Debug("OK connection closed \n") ;
	Debug("OK sync abort : sync_pos %s sync_length %s block_ok %s block_ko %s timing %s\n",
	             $sync_pos,$sync_length,$count_ok,$count_ko,($sync_endtime-$sync_starttime)) ;
	#----------------------------------------------------------------
    }
    close($sock);
    #--------------------------------------------------------------------
} else {
    #--------------------------------------------------------------------
    my $sock = new IO::Socket::INET ( PeerAddr => $options{'destination-server'} , 
				      PeerPort => $options{'destination-port'} , , 
				      Proto => 'tcp' ); 
    if ( ! $sock ) {
	die "Could not connect to ".$options{'destination-server'}.":".$options{'destination-port'}." ( create socket: $! ) \n" ;
    }
    binmode $sock ;
    select($sock) ; $|=1 ; select(STDOUT) ;
    $sock->sockopt(SO_RCVBUF, 32*1048*1024);
    $sock->sockopt(SO_SNDBUF, 32*1048*1024);
    #--------------------------------------------------------------------
    my $sel_sock = IO::Select->new( $sock );
    my $cmd_in_window=0 ;
    #-------------------------------
    my $cmd; 
    if ( $options{'destination-device'} && defined ( $options{'destination-offset'} ) ) {
	$cmd=sprintf("DEVICE %s %s %s %s %s %s",
		     $options{'device'},$sync_length,$options{'blocksize'},uc($options{'mode'}),
		     $options{'destination-device'} , $options{'destination-offset'} ) ;
    } else {
	$cmd=sprintf("DEVICE %s %s %s %s",$options{'device'},$sync_length,$options{'blocksize'},uc($options{'mode'})) ;
    }
    my $res=SendCmdAndWait($sock,$cmd) ;
    if ( $res && $res =~/^\s*OK DEVICE\s*$/ ) {
	Debug("OK SendCmdAndWait DEVICE\n") ;
	#
	$sync_pos=0 ;
	$thr_cksum=threads->new(\&ComputeChecksum,$options{'device'},$options{'offset'},$sync_length) ;
	#
	my $last_display=time()-20 ;
	#
	while ( $sync_pos < $sync_length ) {
	    #
	    my $sync_pos_next_to_send=$sync_pos+$cmd_in_window*$options{'blocksize'} ;
	    #
	    my $must_read=$sel_sock->can_read(0) ;
	    if ( ! defined($must_read) ) {
		$must_read=0 ;
	    }
	    #
	    if ( $block_checksum{$sync_pos_next_to_send} || ( $sync_pos_next_to_send ==  $sync_length  ) || $must_read ) {
		Debug("[debug] sync_pos_next_to_send : %s : sync_length %s : cmd_in_window %s ",$sync_pos_next_to_send,$sync_length,$cmd_in_window) ;
		if ( $must_read ) {
		    my $r=SendCmdAndWait($sock,undef) ;
		    if ( ! defined($r) ) {
			Debug('read is empty !') ;
			Debug("sync_pos :%s sync_pos_next_to_send : %s : sync_length %s : cmd_in_window %s ",$sync_pos,$sync_pos_next_to_send,$sync_length,$cmd_in_window) ;
			last ;
		    }
		    if ( $r =~ /^\s*B$sync_pos\s*$/ ) {
			lock($sync_pos);
			$sync_pos+=$options{'blocksize'} ;
		    } elsif ( $r =~ /^\s*NEED DATA $sync_pos\s*$/ ) {
			&SendBlockData($sock,$options{'device'},$sync_pos) ;
			lock($sync_pos);
			$sync_pos+=$options{'blocksize'} ;
		    }
		    PurgeBlockCheckSum($sync_pos);
		    if ( $cmd_in_window ) {
			$cmd_in_window-- ;
		    }
		} else {
		    Debug("[debug] nothing to read") ;
		    if ( $sync_pos_next_to_send < $sync_length && ( $cmd_in_window < $options{'sync-window'} ) && $block_checksum{$sync_pos_next_to_send} ) {
			my $r=SendCmd($sock,sprintf("C%s:%s:%s",$sync_pos_next_to_send,
						    $block_checksum{$sync_pos_next_to_send}->{'md5'},$block_checksum{$sync_pos_next_to_send}->{'sha1'})) ;
			Debug("[debug] send CKSUM %s : cmd_in_window %s ",$sync_pos_next_to_send,$cmd_in_window) ;
			$cmd_in_window++ ;
		    }
		}
	    } else {
		if ( ( ! $must_read ) && ( $cmd_in_window == 0 || ( ! defined($cksum_pos) ) || ( $sync_pos_next_to_send == $cksum_pos ) ) ) {
		    Debug("[debug] we must wait ... cmd_in_window %s ... sync_pos_next_to_send %s ",$cmd_in_window,$sync_pos_next_to_send) ;
		    if ( defined ( $cksum_pos ) && $sync_pos_next_to_send ) {
			if ( ( time() - $last_display ) > 10 ) {
			    Debug('[notice] ComputeChecksum too slow / Waiting :cksum_pos %s : sync_pos %s (blk %s) : sync_pos_next_to_send :%s ( win %s ) ',
				  $cksum_pos,$sync_pos,scalar(keys %block_checksum),$sync_pos_next_to_send,$cmd_in_window ) ;
			    $last_display=time() ;
			}
			while ( ( ! $must_read  ) && ( $sync_pos_next_to_send == $cksum_pos )) {
			    $must_read=$sel_sock->can_read(0.001) ;
			    if ( ! defined($must_read) ) {
				$must_read=0 ;
			    }
			}
		    } else {
			sleep(1) ;
		    }
		} else {
		    Debug("[debug] we must loop ... cmd_in_window %s ",$cmd_in_window) ;
		}
	    }
	}
	#
	$thr_cksum->join() ;
	Debug("OK sync done \n") ;
	#
    } else {
	Debug("PB sending DEVICE\n") ;
    }
    #
    sleep(10) ;
    #--------------------------------------------------------------------
    close($sock);
    #--------------------------------------------------------------------
}
#----------------------------------------------------------------------------------------------
sub BlockWriter {
    my ($dev,$dev_off,$dat_len)=@_ ;
    my $last_write=0 ;
    Debug('starting BlockWriter thread on %s starting at %s during %s bytes',$dev,$dev_off,$dat_len);
    while ( $last_write < $dev_off+$dat_len-$options{'blocksize'} ) {
	my $what=$queue_4_writing_block->dequeue() ;
	if ( $what eq 'WRT' ) {
	    my $pos=$queue_4_writing_block->dequeue() ;
	    my $dat=$queue_4_writing_block->dequeue() ;
	    &WriteBlockData($dev,$pos,$dat) ;
	    $last_write=$pos ;
	    $pos=undef ;
	    $dat=undef ;
	} elsif ( $what eq 'EOF' ) {
	    Debug('[notice] done thread BlockWriter') ;
	    return ;
 	} else {
	    Debug("[error] BlockWriter bad args '%s'",$what) ;
	}
	$what=undef ;
    }
}
#----------------------------------------------------------------------------------------------
sub WriteBlockData {
    my $d=$_[0] ;
    my $p=$_[1] ;
    my $b=$_[2] ;
    #----------------------
    my $flags=O_RDWR | O_SYNC ;
    eval {
	$flags=O_LARGEFILE | O_RDWR | O_SYNC  ;
    } ;
    if ( ! sysopen(DEVTOWRITE,$d, $flags ) ) {
        Debug("PB open device '%s'\n",$d) ;
        exit 1 ;
    }
    binmode DEVTOWRITE ;
    #----------------------
    sysseek(DEVTOWRITE,$p,0) ;
    my $nwrite=syswrite(DEVTOWRITE,$b,$options{'blocksize'}) ;
    close(DEVTOWRITE) ;
    #----------------------
    if ( $nwrite != $options{'blocksize'} ) {
	use bytes ;
	Debug("PB write device '%s' at '%s' length b %s / nwrite %s \n",$d,$p,length($b),$nwrite) ;
	exit 1;
    }
    #----------------------
}
#----------------------------------------------------------------------------------------------
sub SendBlockData {
    my $s=$_[0] ;
    my $d=$_[1] ;
    my $p=$_[2] ;
    #----------------------
    if ( ! open(DEVTOREAD,"<$d") ) {
        Debug("PB open device '%s'\n",$d) ;
        exit 1 ;
    }
    binmode DEVTOREAD ;
    #----------------------
    my $buf ;
    sysseek(DEVTOREAD,$p,0) ;
    my $nread=sysread(DEVTOREAD,$buf,$options{'blocksize'}) ;
    if ( $nread == $options{'blocksize'} ) {
	if ( ! $options{'compress'} ) {
	    if ( SendCmd($s,"BLOCK DATA $p") ) {
		Debug("[debug] .... starting transmiting block $p  ") ;
		syswrite($s,$buf) ;
	    }
	} else {
	    use bytes ;
	    my $buf_cmp=compress($buf,9) ;
	    if ( SendCmd($s,sprintf("BLOCK DATA COMPRESS ".$p." %d",length($buf_cmp)) ) ) {
		Debug(sprintf("[debug] .... starting transmiting block ".$p." compress %d",length($buf_cmp))) ;
		syswrite($s,$buf_cmp) ;
	    }
	}
    }
    $buf=undef ;
    #----------------------
    close(DEVTOREAD) ;
    #----------------------
    Debug("[debug] .... transmiting .... DONE ") ;
}
#----------------------------------------------------------------------------------------------
sub DisplayProgress {
    #--------------------------------------------------------------------
    #--------------------------------------------------------------------    
}
#----------------------------------------------------------------------------------------------
sub PurgeBlockCheckSum {
    my $p=$_[0] ;
    #
    $p-=$options{'blocksize'} ;
    #
    while ( defined($block_checksum{$p}) ) {
	lock(%block_checksum) ;
	delete $block_checksum{$p}->{'md5'} ;
	delete $block_checksum{$p}->{'sha1'} ;
	delete $block_checksum{$p}->{'pos'} ;
	delete $block_checksum{$p} ;
    }
    #
}
#----------------------------------------------------------------------------------------------
sub ComputeChecksum {
    #--------------------------------------------------------------------
    my ($device_to_read,$device_offset,$sync_size)=($_[0],$_[1],$_[2]) ;
    Debug('starting cksum thread on %s starting at %s during %s bytes',$device_to_read,$device_offset,$sync_size) ;
    #--------------------------------------------------------------------
    my $flags=O_RDONLY ;
    eval {
	$flags=O_LARGEFILE | O_RDONLY ;
    } ;
    if ( ! sysopen(DEVCKSUM,$device_to_read, $flags )  ) {
	Debug("PB open device '%s'\n",$device_to_read) ;
	exit 1 ;
    }
    $cksum_pos=0 ;
    binmode DEVCKSUM ;
    #--------------------------------------------------------------------
    my $factor=(268435456/$options{'blocksize'})*$options{'bufferfactor'} ;
    #--------------------------------------------------------------------
    while ( $cksum_pos  < $sync_size ) {
	if ( $cksum_pos > $sync_pos + $factor*$options{'blocksize'} ) {
	    Debug('[notice] ComputeChecksum too fast / Waiting %s vs %s (%s) ',$cksum_pos,$sync_pos,scalar(keys %block_checksum)) ;
	    sleep(4) ;
	    my $do_wait=8 ;
	    while ( ( $cksum_pos > $sync_pos + ($factor*$options{'blocksize'})/3 ) && ( $do_wait-- ) ) {
		sleep(2);
	    }
	} elsif ( $queue_4_writing_block->pending()  > 40 ) {
	    Debug('[notice] ComputeChecksum too agressive / Writer pending %s / Waiting %s vs %s (%s) ',$queue_4_writing_block->pending(),
		                                                                                        $cksum_pos,$sync_pos,scalar(keys %block_checksum)) ;
	    sleep(($queue_4_writing_block->pending()/20)) ;
	} else {
	    my $buf ;
	    if ( ! sysseek(DEVCKSUM,$device_offset+$cksum_pos,0) ) {
		Debug("pb during seek at %s error %s\n",$device_offset+$cksum_pos,$!) ;
		exit 1 ;
	    }
	    my $nread=sysread(DEVCKSUM,$buf,$options{'blocksize'}) ;
	    if ( defined($nread) ) {
		if ( $nread == $options{'blocksize'} ) {
		    my %h : shared  ;
		    $h{'pos'}=$cksum_pos ;
		    if ( $options{'mode'} eq 'ultra' ) {
			$h{'md5'}=substr(encode_base64(edonr_512($buf),undef),0,11) ;
			$h{'sha1'}='X' ;
		    } elsif ( $options{'mode'} eq 'fast' ) {
			$h{'md5'}=encode_base64(md4($buf),undef) ;
			$h{'sha1'}=encode_base64(edonr_384($buf),undef) ;
		    } elsif ( $options{'mode'} eq 'standart' ) {
			$h{'md5'}=encode_base64(md5($buf),undef) ;
			$h{'sha1'}=encode_base64(sha1($buf),undef) ;
		    } else {
			$h{'md5'}=encode_base64(skein_1024($buf),undef) ;
			$h{'sha1'}=encode_base64(sha512($buf),undef) ;
		    }
		    lock(%block_checksum) ;
		    $block_checksum{$cksum_pos}=\%h ;
		    $cksum_pos=$cksum_pos+$options{'blocksize'} ;
		} else {
		    Debug("pb during read it got only %s instead %s\n",$nread,$options{'blocksize'}) ;
		    exit 1 ;
		}
	    } else {
		Debug("pb during read it %s error %s\n",$device_offset+$cksum_pos,$!) ;
		exit 1 ;
	    }
	    $buf=undef ;
	}
    }
    close(DEVCKSUM) ;
    #--------------------------------------------------------------------
    Debug('done cksum thread on %s',$device_to_read) ;
}
#----------------------------------------------------------------------------------------------
sub DeviceSize {
    if ( -b $_[0] ) {
	my $sb =`blockdev  --getsize64 $_[0] 2>&1 ` ;
        chomp($sb) ;
	my $sbk ;
        if ( $sb !~ /^\d+$/ ) {
	    $sb =`blockdev  --getsize $_[0] 2>&1 ` ;
	    chomp($sb) ;
	    $sbk=$sb ;
	    $sb=$sbk*512 ;
	} else {
	    $sbk=$sb/512 ;
	}
	Debug("for device %s size is  %s bytes or %s blocks(512bytes)\n",$_[0],$sb,$sbk ) ;
	return ( $sb,$sbk) ;
    } elsif ( -f $_[0] && $_[1]) {
	my @infof=(stat($_[0]))[7] ;
	if ( ( ( $infof[0] % 512 ) != 0 ) ) {
	    die(sprintf("for file %s , size in byte ( %d ) is a multiple of 512 \n",$_[0],$infof[0])) ;
	}
	push @infof,$infof[0]/512 ;
	Debug("for file %s size is  %s bytes or %s blocks(512bytes)\n",$_[0],$infof[0],$infof[1]) ;
	return @infof ;
    } else {
	return (0,0) ;
    }
}
#----------------------------------------------------------------------------------------------
sub ReadCmd {
    my $s = $_[0] ;
    my $b ;
    my $l ;
    my $s2r ;
    my $inr ;
    my $nr ;
    #----------------------------------
    $s2r=4 ;
    $nr=0 ;
    $inr=sysread($s,$b,$s2r) ;
    while ( $inr ) {
	$nr+=$inr ;
	$s2r-=$inr ;
	if ( $s2r ) {
	    $inr=sysread($s,$b,$s2r,$nr) ;
	} else {
	    $inr=0 ;
	}
    }
    if ( $nr != 4 ) {
	Debug("[error] KO ReadCmd 4 vs %s\n",$nr) ;
	return undef ;
    }
    #----------------------------------
    $l = hex ("0x".substr($b,1,3)) ;
    #
    $s2r=$l ;
    $nr=0 ;
    $inr=sysread($s,$b,$s2r) ;
    while ( $inr ) {
	$nr+=$inr ;
	$s2r-=$inr ;
	if ( $s2r ) {
	    $inr=sysread($s,$b,$s2r,$nr) ;
	} else {
	    $inr=0 ;
	}
    }
    if ( $nr != $l ) {
	Debug("[error] KO ReadCmd must read %s but got %s\n",$l,$nr) ;
	return undef ;
    }
    #----------------------------------
    Debug("[debug] OK ReadCmd %s\n",$b) ;
    return $b ;
    #----------------------------------
}
#----------------------------------------------------------------------------------------------
sub SendCmd {
    my $s = $_[0] ;
    my $t = $_[1] ;
    my $tt ;
    #----------------------------------
    if ( ! defined($t) ) {
	return 1 ;
    }
    #----------------------------------
    $tt=sprintf("C%03x%s",length($t),$t) ;
    #----------------------------------
    if ( print $s $tt ) {
	Debug("[debug] OK SendCmd '%s'\n",$t) ;
	return 1 ;
    } else {
	Debug("[error] KO SendCmd '%s'\n",$t) ;
	return undef ;
    }
    #----------------------------------
}
#----------------------------------------------------------------------------------------------
sub SendCmdAndWait {
    my $s = $_[0] ;
    my $t = $_[1] ;
    #----------------------------------
    if ( SendCmd($s,$t) ) {
	return ReadCmd($s) ; 
    } else {
	return undef ;
    }
    #----------------------------------
}
#----------------------------------------------------------------------------------------------
sub ReadOffset {
    my $o ;
    print $_[0] ;
    while ( ! $o ) {
	$o=<> ;
	chomp($o) ;
	if ( $o =~ /^[0-9]+$/ ) {
	    return $o ;
	}
	$o=undef ;
	print $_[0] ;
    }
}
#----------------------------------------------------------------------------------------------
sub ReadDevice {
    my $d ;
    print $_[0] ;
    while ( ! $d ) {
	$d=<> ;
	chomp($d) ;
	if ( -b $d ) {
	    return $d ;
	}
	$d=undef ;
	print $_[0] ;
    }
}
#----------------------------------------------------------------------------------------------
# extrait de syslog.conf
# debug, info, notice, warning , error
#
sub Debug {
    #
    if (! $debug ) {
        return undef ;
    }
    #
    my $txt_to_print  ;
    #
    if ( scalar(@_) > 1 ) {
        $txt_to_print=sprintf($_[0],@_[1..(scalar(@_)-1)]) ;
    } else {
        $txt_to_print=$_[0] ;
    }
    #
    my $level ;
    if ( $txt_to_print =~ /^\s*\[((debug)|(notice)|(info)|(warning)|(error))\]\s*([^\s].*)$/i ) {
        $txt_to_print=sprintf("[%-7s] %s",uc($1),$7) ;
        $level=lc($1) ;
    } elsif ( $txt_to_print =~ /^\s*\[(\[[^\]]*\])\]\s*([^\s].*)$/i ) {
        $txt_to_print=sprintf("[BOGUS  ] %s",$txt_to_print) ;
        $level='bogus' ;
    } else {
        $txt_to_print=sprintf("[INFO   ] %s",$txt_to_print) ;
        $level='info' ;
    }
    #
    if ( $debug_level_2_print{$level} > $debug ) {
        return undef ;
    }
    #
    my $txt_time=sprintf("[%02d:%02d:%02d] ",(localtime())[2,1,0]) ;
    #
    $txt_to_print=join("\n", map {$_=$txt_time.$_ ; }  (split(/\n/,$txt_to_print)) ) ;
    #
    chomp($txt_to_print) ;
    $txt_to_print.="\n" ;
    #
    print STDERR $txt_to_print ;
    #
}
#------------------------------------------------------------------------
1;
__END__
#------------------------------------------------------------------------
scp lotar.emotic.info:/usr/local/bin/SyncDevice .
/usr/local/bin/SyncDevice --device /dev/xen_hdd/mylar --destination-server 10.3.0.1 --verbose
/dev/drbd2
#------------------------------------------------------------------------
/dev/lotar/Grub-Etherboot-ISO
lvcreate -n TestSyncDevice --size 15M xen_hdd
lvcreate -n TestSyncDevice --size 17M lotar
dd if=/dev/random of=/dev/xen_hdd/TestSyncDevice bs=512

/usr/local/bin/SyncDevice --device /dev/xen_hdd/TestSyncDevice --destination-server 10.3.0.1 --verbose
/dev/lotar/TestSyncDevice
32256

#------------------------------------------------------------------------

pb conso memoirre avec des petit blocks
pb thread de cksum qu il faut arreter quand le client abort
prevoir une progress bar



prevoir une purge


OK SendCmd BLOCK 14898167808 OK
#------------------------------------------------------------------------
DEVICE <remote device> <lengths byte> <blocksize> (ULTRA|FAST|SECURE) <local device> <local offset>

C <pos> <digest1> <digest2>
B <pos> OK
NEED DATA <pos>

BLOCK DATA <pos>
BLOCK DATA COMPRESS <pos> <compressed size>

#----



#----

CKSUM PARTIAL <pos> <size> <digest1> <digest2>
BLOCK PARTIAL <pos> <partial size> OK
NEED DATA PARTIAL <pos> <partial size> 

BLOCK DATA PARTIAL <pos> <partial size>
BLOCK DATA PARTIAL COMPRESS <pos> <partial size>

#------------------------------------------------------------------------
